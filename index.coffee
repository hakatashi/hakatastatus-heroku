Twitter = require './twitter'
async = require 'async'

twitter = new Twitter
  oauth:
    consumer_key: process.env.CONSUMER_KEY
    consumer_secret: process.env.CONSUMER_SECRET
    hakatastatus:
      oauth_token: process.env.OAUTH_TOKEN
      oauth_token_secret: process.env.OAUTH_TOKEN_SECRET

tweet = (text) ->
  console.log "Tweet: #{text}"
  twitter.post 'hakatastatus', 'statuses/update',
    status: "[Status Report #{new Date().toLocaleString()}]\n#{text}"[...140]

emoji = (status) ->
  if status is 'success'
    emojies = [
      'âœ”ï¸' # Heavy Check Mark
      'â˜‘ï¸' # Ballot Box With Check
      'ðŸ‘Œ' # Ok Hand Sign
      'ðŸ™†' # Face With OK Gesture
      'ðŸ‘' # Thumbs Up Sign
      'ðŸ†—' # Squared OK
      'â­•' # Heavy Large Circle
      'ðŸ™‹' # Happy Person Raising One Hand
      'ðŸ˜' # Smiling Face With Heart-Shaped Eyes
      'ðŸ˜Š' # Smiling Face With Smiling Eyes
    ]
  else if status is 'failed'
    emojies = [
      'ðŸ‘Ž' # Thumbs Down Sign
      'ðŸ†–' # Squared NG
      'ðŸ‘Š' # Fisted Hand Sign
      'ðŸ™…' # Face With No Good Gesture
      'âŒ' # Cross Mark
      'ðŸ™Ž' # Person With Pouting Face
      'ðŸ˜¡' # Pouting Face
      'ðŸ˜±' # Face Screaming In Fear
      'ðŸ˜µ' # Dizzy Face
    ]
  else emojies = []

  return emojies[Math.floor Math.random() * emojies.length]

checkers = ['glyphwikibot']

async.map checkers, (checkerName, done) ->
  checker = require "./checkers/#{checkerName}"
  checker twitter, done
, (error, statuses) ->

  return tweet("@hakatashi Status Monitor crached! #{emoji 'failed'}") if error

  everythingIsOperatinal = yes
  for status, index in statuses
    if status
      checker = checkers[index]
      tweet "#{emoji 'failed'}#{checker}: @hakatashi #{status.message}"
      everythingIsOperatinal = no

  if everythingIsOperatinal
    tweet "#{emoji 'success'} Everything is operational!"

  return
